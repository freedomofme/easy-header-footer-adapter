buildscript {
    ext {
        preDexLibs = !project.hasProperty('disablePreDex')

        version = '3.0.0'
        versionCode = 11
        minSdkVersion = 14
        targetSdkVersion = 26
        compileSdkVersion = 26
        buildToolsVersion = '26.0.1'

        androidPluginVersion = '3.0.0-alpha9'
        freefairPluginsVersion = '1.0.0'

        supportLibraryVersion = '26.0.0'

        deps = [
                supportAppCompat   : "com.android.support:appcompat-v7:$supportLibraryVersion",
                supportRecyclerView: "com.android.support:recyclerview-v7:$supportLibraryVersion",
                supportAnnotations : "com.android.support:support-annotations:$supportLibraryVersion"
        ]
    }

    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$androidPluginVersion"
    }
}

allprojects {
    version = rootProject.ext.version

    repositories {
        jcenter()
        google()
    }
}

subprojects {
    def appPlugin = "com.android.build.gradle.AppPlugin"
    def libraryPlugin = "com.android.build.gradle.LibraryPlugin"

    project.plugins.whenPluginAdded { plugin ->
        if (appPlugin == plugin.class.name || libraryPlugin == plugin.class.name) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}
