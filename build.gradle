ext {
    preDexLibs = !project.hasProperty('disablePreDex')

    version = '2.0.0'
    versionCode = 7
    minSdkVersion = 9
    targetSdkVersion = 25
    compileSdkVersion = 25
    buildToolsVersion = '25.0.3'

    supportLibraryVersion = '25.3.1'

    deps = [
            supportAppCompat   : "com.android.support:appcompat-v7:$supportLibraryVersion",
            supportRecyclerView: "com.android.support:recyclerview-v7:$supportLibraryVersion",
            supportAnnotations : "com.android.support:support-annotations:$supportLibraryVersion"
    ]
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.2'
    }
}

allprojects {
    version = rootProject.ext.version

    repositories {
        jcenter()
    }
}

subprojects {
    def appPlugin = "com.android.build.gradle.AppPlugin"
    def libraryPlugin = "com.android.build.gradle.LibraryPlugin"

    project.plugins.whenPluginAdded { plugin ->
        if (appPlugin == plugin.class.name || libraryPlugin == plugin.class.name) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}
